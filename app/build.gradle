apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.mck.icndbclient"
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            if (System.console() != null) {
                storeFile file("keystore/icndbclientkey.keystore")
                storePassword System.console().readLine("\nKeystore password: ")
                keyAlias System.console().readLine("\nKey alias: ")
                keyPassword System.console().readLine("\nKey password: ")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // atm, i am only using console to sign and release. A release
            // only happens with a signing config.
            if (System.console() != null) {
                signingConfig signingConfigs.release
            }
        }

        millhouse.initWith(buildTypes.release)
        millhouse {
            applicationIdSuffix ".release.millhouse"
        }

        calrissian.initWith(buildTypes.release)
        calrissian {
            applicationIdSuffix ".release.calrissian"
        }
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
    }

    /**
     * Product flavors seem to go with the aosp
     * concept of Multiple APKs. Documentation suggests
     * not using multiple apks unless necessary.
     * http://developer.android.com/google/play/publishing/multiple-apks.html
     * more info about using multiple akps can be found at
     * https://developer.android.com/training/multiple-apks/index.html
     */
    /*productFlavors {
        support {
            // uses the default.
        }
        kitkat {
            minSdkVersion 19
        }
    }*/
}

repositories{
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    compile 'com.android.support:appcompat-v7:22.0.0'

    // unit tests should include
    testCompile 'junit:junit:4.12'

    // Need to use a partial mock (spy) so must use version 1.9.5 .
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'org.skyscreamer:jsonassert:1.2.3'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.3.1'
}
