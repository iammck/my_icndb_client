apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.mck.icndbclient"
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 3
        versionName "3.0"
    }

    signingConfigs {
        release {
            if (System.console() != null) {
                storeFile file("keystore/icndbclientkey.keystore")
                storePassword System.console().readLine("\nKeystore password: ")
                keyAlias System.console().readLine("\nKey alias: ")
                keyPassword System.console().readLine("\nKey password: ")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // atm, i am only using console to sign and release. A release
            // only happens with a signing config.
            if (System.console() != null) {
                signingConfig signingConfigs.release
            }
        }

        millhouseDebug.initWith(buildTypes.debug)
        millhouseDebug {
            applicationIdSuffix ".debug.millhouse"
        }

        calrissianDebug.initWith(buildTypes.debug)
        calrissianDebug {
            applicationIdSuffix ".debug.calrissian"
        }

        millhouseRelease.initWith(buildTypes.release)
        millhouseRelease {
            applicationIdSuffix ".release.millhouse"
        }

        calrissianRelease.initWith(buildTypes.release)
        calrissianRelease {
            applicationIdSuffix ".release.calrissian"
        }
    }
    // They use custom source sets.
    sourceSets {
        millhouseDebug.setRoot('src/millhouse')
        millhouseRelease.setRoot('src/millhouse')
        calrissianDebug.setRoot('src/calrissian')
        calrissianRelease.setRoot('src/calrissian')
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
    }

    /**
     * Product flavors seem to go with the aosp
     * concept of Multiple APKs. Documentation suggests
     * not using multiple apks unless necessary.
     * http://developer.android.com/google/play/publishing/multiple-apks.html
     * more info about using multiple akps can be found at
     * https://developer.android.com/training/multiple-apks/index.html
     *
     * I am going to use product flavors to make upgrades to the UI. Plus,
     * I am interested in knowing if like build types a class defined in the
     * src/main/java directory can not be redefined in the src/flavor/java.
     * (For build types if src/main/java/MyClass.java exits, creating a
     * src/buildType/MyClass.java will raise compile time errors because
     * the buildType is initialized with a debug release build types).
     *
     * unlike the calrissianRelease or millhouse build types, upgrades to the UI
     * are not creating a different application, but are different versions
     * of the same application. That makes them good canidates for flavors.
     *
     * Product flavor for using a button or a ViewPager with adapter are completed
     * and commented out. SlidingJokesFlavor is the combination of the two flavors.
     */
    productFlavors {
        //ButtonActivityFlavor {}
        //SliderActivityFlavor {}
        SlidingJokesFlavor {}
    }
}

repositories{
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    compile 'com.android.support:appcompat-v7:22.0.0'

    // unit tests should include
    testCompile 'junit:junit:4.12'

    // Need to use a partial mock (spy) so must use version 1.9.5 .
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'org.skyscreamer:jsonassert:1.2.3'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.3.1'
}
